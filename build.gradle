plugins {
	id 'org.jetbrains.kotlin.jvm' version '2.2.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.jooq.jooq-codegen-gradle' version '3.19.28'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Book Management Application'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-flyway'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	runtimeOnly 'org.postgresql:postgresql'
	jooqCodegen 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-jooq-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict', '-Xannotation-default-target=param-property'
	}
}

sourceSets {
	main {
		java {
			srcDir 'build/generated-src/jooq/main'
		}
	}
}

Properties localProps = new Properties()
File localPropsFile = rootProject.file('local.properties')
if (localPropsFile.exists()) {
	localPropsFile.withInputStream{localProps.load(it) }
}

def resolveSecretSetting = { propKey, envKey, defaultValue ->
	localProps.getProperty(propKey) ?:
			(System.getenv(envKey) ?: defaultValue)
}

def codegenJdbcUrl = resolveSecretSetting('CODEGEN_JDBC_URL', 'CODEGEN_JDBC_URL', 'jdbc:postgresql://url')
def codegenJdbcUser = resolveSecretSetting('CODEGEN_JDBC_USER', 'CODEGEN_JDBC_USER', 'username')
def codegenJdbcPassword = resolveSecretSetting('CODEGEN_JDBC_PASSWORD', 'CODEGEN_JDBC_PASSWORD', 'password')

jooq {
	configuration {
		logging = org.jooq.meta.jaxb.Logging.WARN
		jdbc {
			driver = 'org.postgresql.Driver'
			url = codegenJdbcUrl
			user = codegenJdbcUser
			password = codegenJdbcPassword
		}
		generator {
            name = 'org.jooq.codegen.KotlinGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                includes = '.*'
                excludes = 'flyway_schema_history'
            }
            target {
                packageName = 'com.example.book_manager.jooq'
                directory = 'build/generated-src/jooq'
            }
        }
	}
}

tasks.named('compileKotlin') {
	dependsOn 'jooqCodegen'
}

tasks.named('test') {
	useJUnitPlatform()
}
